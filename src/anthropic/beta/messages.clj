(ns anthropic.beta.messages
  "Clojure wrapper for the beta Messages API."
  (:require [anthropic.core]
            [anthropic.pagination :as pagination]
            [anthropic.stream :as stream-control]
            [clojure.string :as str]
            [clojure.walk :as walk]
            [jsonista.core :as json])
  (:import (com.anthropic.client AnthropicClient)
           (com.anthropic.core JsonValue RequestOptions)
           (com.anthropic.core.http Headers HttpResponse HttpResponseFor StreamResponse)
           (com.anthropic.errors AnthropicException)
           (com.anthropic.helpers BetaToolRunner)
           (com.anthropic.models.beta.messages BetaBase64ImageSource
                                               BetaBase64ImageSource$MediaType
                                               BetaBase64PdfSource
                                               BetaCacheControlEphemeral
                                               BetaCacheControlEphemeral$Ttl
                                               BetaContainerParams
                                               BetaContentBlockParam
                                               BetaContextManagementConfig
                                               BetaCountTokensContextManagementResponse
                                               BetaContextManagementConfig$Edit
                                               BetaClearThinking20251015Edit
                                               BetaClearToolUses20250919Edit
                                               BetaCompact20260112Edit

                                               BetaDiagnosticsParam
                                               BetaInputTokensTrigger

                                               BetaImageBlockParam
                                               BetaImageBlockParam$Source
                                               BetaJsonOutputFormat
                                               BetaJsonOutputFormat$Schema
                                               BetaMessage BetaMessageTokensCount
                                               BetaMessageParam BetaMessageParam$Builder
                                               BetaMessageParam$Role BetaMessageParam$ClearAt
                                               BetaMetadata BetaOutputConfig
                                               BetaOutputConfig$Effort
                                               StructuredOutputConfig
                                               BetaFallbackParam
                                               BetaFallbackParam$Speed
                                               BetaFileDocumentSource
                                               BetaFileImageSource
                                               BetaImageTransformationsParam
                                               BetaImageTransformationsParam$OversizedImage
                                               BetaPlainTextSource
                                               BetaRedactedThinkingBlockParam
                                               BetaRequestDocumentBlock
                                               BetaRequestDocumentBlock$Source
                                               BetaRequestToolAdditionBlock
                                               BetaRequestToolAdditionBlock$Builder
                                               BetaRequestToolRemovalBlock
                                               BetaRequestToolRemovalBlock$Builder
                                               BetaRequestMcpServerToolConfiguration
                                               BetaRequestMcpServerUrlDefinition
                                               BetaTextBlockParam
                                               BetaTokenTaskBudget
                                               BetaThinkingBlockParam
                                               BetaThinkingConfigAdaptive
                                               BetaThinkingConfigDisabled
                                               BetaThinkingConfigEnabled
                                               BetaThinkingConfigParam
                                               BetaSkillParams
                                               BetaSkillParams$Type
                                               BetaTool BetaTool$Builder BetaTool$AllowedCaller BetaTool$InputSchema
                                               BetaTool$InputSchema$Properties
                                               BetaToolUnion
                                               BetaWebSearchTool20260318
                                               BetaWebFetchTool20260318
                                               BetaCodeExecutionTool20260521
                                               BetaToolBash20250124
                                               BetaToolTextEditor20250728
                                               BetaMemoryTool20250818
                                               BetaToolBash20250124$InputExample$Builder
                                               BetaToolTextEditor20250728$InputExample$Builder
                                               BetaMemoryTool20250818$InputExample$Builder
                                               BetaToolSearchToolBm25_20251119$AllowedCaller
                                               BetaToolSearchToolBm25_20251119$Type
                                               BetaToolSearchToolRegex20251119$AllowedCaller
                                               BetaToolSearchToolRegex20251119$Type
                                               BetaToolSearchToolBm25_20251119
                                               BetaToolSearchToolRegex20251119
                                               BetaToolComputerUse20251124
                                               BetaToolComputerUse20251124$AllowedCaller
                                               BetaToolComputerUse20251124$Builder
                                               BetaToolComputerUse20251124$InputExample$Builder
                                               BetaAdvisorTool20260301
                                               BetaAdvisorTool20260301$AllowedCaller
                                               BetaAdvisorTool20260301$Builder
                                               BetaMcpToolset BetaMcpToolset$Configs
                                               BetaMcpToolDefaultConfig
                                               BetaBrowserToolset20260801
                                               BetaBrowserToolsetConfigs BetaBrowserToolsetConfigs$Builder
                                               BetaBrowserCloseTabConfig BetaBrowserDoubleClickConfig
                                               BetaBrowserFileUploadConfig BetaBrowserFindConfig
                                               BetaBrowserFormInputConfig BetaBrowserGetPageTextConfig
                                               BetaBrowserHoldKeyConfig BetaBrowserHoverConfig
                                               BetaBrowserJavascriptExecConfig BetaBrowserKeyConfig
                                               BetaBrowserLeftClickConfig BetaBrowserLeftClickDragConfig
                                               BetaBrowserLeftMouseDownConfig BetaBrowserLeftMouseUpConfig
                                               BetaBrowserListTabsConfig BetaBrowserMiddleClickConfig
                                               BetaBrowserMouseMoveConfig BetaBrowserNavigateConfig
                                               BetaBrowserNewTabConfig BetaBrowserReadConsoleConfig
                                               BetaBrowserReadNetworkConfig BetaBrowserReadPageConfig
                                               BetaBrowserRightClickConfig BetaBrowserScreenshotConfig
                                               BetaBrowserScrollConfig BetaBrowserScrollToConfig
                                               BetaBrowserSwitchTabConfig BetaBrowserTripleClickConfig
                                               BetaBrowserTypeConfig BetaBrowserWaitConfig BetaBrowserZoomConfig
                                               BetaComputerToolset20260801
                                               BetaComputerToolsetConfigs BetaComputerToolsetConfigs$Builder
                                               BetaComputerCursorPositionConfig BetaComputerDoubleClickConfig
                                               BetaComputerHoldKeyConfig BetaComputerKeyConfig
                                               BetaComputerLeftClickConfig BetaComputerLeftClickDragConfig
                                               BetaComputerLeftMouseDownConfig BetaComputerLeftMouseUpConfig
                                               BetaComputerMiddleClickConfig BetaComputerMouseMoveConfig
                                               BetaComputerRightClickConfig BetaComputerScreenshotConfig
                                               BetaComputerScrollConfig BetaComputerTripleClickConfig
                                               BetaComputerTypeConfig BetaComputerWaitConfig BetaComputerZoomConfig
                                               BetaCitationsConfigParam BetaUserLocation
                                               BetaWebSearchTool20260318$AllowedCaller
                                               BetaWebSearchTool20260318$Builder
                                               BetaWebSearchTool20260318$ResponseInclusion
                                               BetaWebFetchTool20260318$AllowedCaller
                                               BetaWebFetchTool20260318$Builder
                                               BetaWebFetchTool20260318$ResponseInclusion
                                               BetaCodeExecutionTool20260521$AllowedCaller
                                               BetaCodeExecutionTool20260521$Builder
                                               BetaToolBash20250124$AllowedCaller
                                               BetaToolBash20250124$Builder
                                               BetaToolTextEditor20250728$AllowedCaller
                                               BetaToolTextEditor20250728$Builder
                                               BetaMemoryTool20250818$AllowedCaller
                                               BetaMemoryTool20250818$Builder
                                               BetaTool$InputExample$Builder
                                               BetaToolChangeMcpToolReference
                                               BetaToolChoice BetaToolChoiceAny
                                               BetaToolChoiceAuto BetaToolChoiceNone
                                               BetaToolChoiceTool
                                               BetaToolResultBlockParam
                                               BetaToolUseBlockParam
                                               BetaToolUseBlockParam$Input
                                               BetaUrlImageSource BetaUrlPdfSource
                                               MessageCountTokensParams
                                               MessageCountTokensParams$Tool
                                               MessageCountTokensParams$Builder
                                               MessageCountTokensParams$Speed
                                               MessageCreateParams
                                               MessageCreateParams$Builder
                                               MessageCreateParams$ServiceTier
                                               MessageCreateParams$Speed
                                               BetaRawMessageStreamEvent)
           (com.anthropic.models.beta.messages.batches BatchCreateParams
                                                       BatchCreateParams$Request
                                                       BatchCreateParams$Request$Params
                                                       BatchCreateParams$Request$Params$Builder
                                                       BatchCreateParams$Request$Params$ServiceTier
                                                       BatchDeleteParams BatchListPage BatchListParams
                                                       BetaDeletedMessageBatch BetaMessageBatch
                                                       BetaMessageBatchIndividualResponse)
           (com.anthropic.services.blocking.beta.messages BatchService)))

(set! *warn-on-reflection* true)

(def ^:private throw-normalized! @#'anthropic.core/throw-normalized!)
(def ^:private json-mapper (json/object-mapper {:decode-key-fn true}))

(defmacro ^:private with-api-errors [& body]
  `(binding [pagination/*error-handler*
             (fn [e#]
               (if (instance? AnthropicException e#)
                 (throw-normalized! e#)
                 (throw e#)))]
     (try ~@body
          (catch AnthropicException e# (throw-normalized! e#)))))

(defn- validate-allowed-domains! [t]
  (when (and (contains? t :allowed-domains)
             (empty? (:allowed-domains t)))
    (throw (ex-info "Allowed domains must contain at least one domain"
                    {:anthropic/error :empty-allowed-domains
                     :allowed-domains []}))))

(defn- ->json ^JsonValue [x]
  (JsonValue/from (walk/stringify-keys x)))

(defn- ->wire-data [x]
  (cond
    (map? x) (into {} (map (fn [[k v]] [(name k) (->wire-data v)])) x)
    (sequential? x) (mapv ->wire-data x)
    (keyword? x) (str/replace (name x) "-" "_")
    :else x))

(defn- ->cache-control ^BetaCacheControlEphemeral [cc]
  (let [b (BetaCacheControlEphemeral/builder)]
    (when-let [ttl (and (map? cc) (:ttl cc))]
      (.ttl b (BetaCacheControlEphemeral$Ttl/of (name ttl))))
    (.build b)))

(defn- ->image-source ^BetaImageBlockParam$Source [{:keys [type media-type data url file-id]}]
  (case (keyword type)
    :base64 (BetaImageBlockParam$Source/ofBase64
             (-> (BetaBase64ImageSource/builder)
                 (.data ^String data)
                 (.mediaType (BetaBase64ImageSource$MediaType/of ^String media-type))
                 (.build)))
    :url (BetaImageBlockParam$Source/ofUrl
          (-> (BetaUrlImageSource/builder) (.url ^String url) (.build)))
    :file (BetaImageBlockParam$Source/ofFile
           (-> (BetaFileImageSource/builder) (.fileId ^String file-id) (.build)))
    (throw (ex-info "Unsupported image source type"
                    {:anthropic/error :unsupported-content-source :type type}))))

(defn- ->document-source ^BetaRequestDocumentBlock$Source [{:keys [type data url file-id]}]
  (case (keyword type)
    :base64 (BetaRequestDocumentBlock$Source/ofBase64
             (-> (BetaBase64PdfSource/builder) (.data ^String data) (.build)))
    :url (BetaRequestDocumentBlock$Source/ofUrl
          (-> (BetaUrlPdfSource/builder) (.url ^String url) (.build)))
    :file (BetaRequestDocumentBlock$Source/ofFile
           (-> (BetaFileDocumentSource/builder) (.fileId ^String file-id) (.build)))
    :text (BetaRequestDocumentBlock$Source/ofText
           (-> (BetaPlainTextSource/builder) (.data ^String data) (.build)))
    (throw (ex-info "Unsupported document source type"
                    {:anthropic/error :unsupported-content-source :type type}))))

(defn- ->beta-image-transformations ^BetaImageTransformationsParam [{:keys [oversized-image]}]
  (let [b (BetaImageTransformationsParam/builder)]
    (when oversized-image
      (.oversizedImage b
                       (BetaImageTransformationsParam$OversizedImage/of
                        (name oversized-image))))
    (.build b)))

(defn- ->beta-skill-params ^BetaSkillParams [{:keys [skill-id type version]}]
  (let [b (-> (BetaSkillParams/builder)
              (.skillId ^String skill-id)
              (.type (BetaSkillParams$Type/of (name type))))]
    (when version (.version b ^String version))
    (.build b)))

(defn- ->beta-container-params ^BetaContainerParams [{:keys [id skills]}]
  (let [b (BetaContainerParams/builder)]
    (when id (.id b ^String id))
    (when skills (.skills b ^java.util.List (mapv ->beta-skill-params skills)))
    (.build b)))

(declare ->citations)

(defn- ->sdk-text-block ^BetaTextBlockParam [blk]
  (.readValue (JsonValue/access$getJSON_MAPPER$cp)
              (json/write-value-as-string (->wire-data blk))
              BetaTextBlockParam))

(defn- ->system-block ^BetaTextBlockParam [{:keys [text cache-control] :as blk}]
  (if (contains? blk :citations)
    (->sdk-text-block blk)
    (let [b (-> (BetaTextBlockParam/builder) (.text ^String text))]
      (when cache-control (.cacheControl b (->cache-control cache-control)))
      (.build b))))

(defn- ->tool-input ^BetaToolUseBlockParam$Input [input]
  (let [b (BetaToolUseBlockParam$Input/builder)]
    (doseq [[k v] input]
      (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- add-tool-change
  [^BetaRequestToolAdditionBlock$Builder b {:keys [reference mcp-tool-reference mcp-toolset-reference]}]
  (cond
    reference (.referenceTool b ^String reference)
    mcp-tool-reference
    (.tool b ^BetaToolChangeMcpToolReference
           (-> (BetaToolChangeMcpToolReference/builder)
               (.name ^String (:name mcp-tool-reference))
               (.serverName ^String (:server-name mcp-tool-reference))
               (.build)))
    mcp-toolset-reference (.mcpToolsetReferenceTool b ^String (:server-name mcp-toolset-reference))
    :else (throw (ex-info "Unsupported beta tool change reference"
                          {:anthropic/error :unsupported-tool-change-reference}))))

(defn- remove-tool-change
  [^BetaRequestToolRemovalBlock$Builder b {:keys [reference mcp-tool-reference mcp-toolset-reference]}]
  (cond
    reference (.referenceTool b ^String reference)
    mcp-tool-reference
    (.tool b ^BetaToolChangeMcpToolReference
           (-> (BetaToolChangeMcpToolReference/builder)
               (.name ^String (:name mcp-tool-reference))
               (.serverName ^String (:server-name mcp-tool-reference))
               (.build)))
    mcp-toolset-reference (.mcpToolsetReferenceTool b ^String (:server-name mcp-toolset-reference))
    :else (throw (ex-info "Unsupported beta tool change reference"
                          {:anthropic/error :unsupported-tool-change-reference}))))

(defn- ->content-block ^BetaContentBlockParam [{:keys [type cache-control] :as blk}]
  (case (keyword type)
    :text (if (contains? blk :citations)
            (BetaContentBlockParam/ofText (->sdk-text-block blk))
            (let [b (-> (BetaTextBlockParam/builder) (.text ^String (:text blk)))]
              (when cache-control (.cacheControl b (->cache-control cache-control)))
              (BetaContentBlockParam/ofText (.build b))))
    :image (let [b (-> (BetaImageBlockParam/builder)
                        (.source ^BetaImageBlockParam$Source (->image-source (:source blk))))]
             (when cache-control (.cacheControl b (->cache-control cache-control)))
             (when-let [transformations (:transformations blk)]
               (.transformations b (->beta-image-transformations transformations)))
             (BetaContentBlockParam/ofImage (.build b)))
    :document (let [b (-> (BetaRequestDocumentBlock/builder)
                           (.source ^BetaRequestDocumentBlock$Source (->document-source (:source blk))))]
                (when cache-control (.cacheControl b (->cache-control cache-control)))
                (when-let [title (:title blk)] (.title b ^String title))
                (when-let [context (:context blk)] (.context b ^String context))
                (when (contains? blk :citations)
                  (.citations b ^BetaCitationsConfigParam (->citations (:citations blk))))
                (BetaContentBlockParam/ofDocument (.build b)))
    :thinking (BetaContentBlockParam/ofThinking
               (-> (BetaThinkingBlockParam/builder)
                   (.thinking ^String (:thinking blk))
                   (.signature ^String (:signature blk))
                   (.build)))
    :redacted-thinking (BetaContentBlockParam/ofRedactedThinking
                        (-> (BetaRedactedThinkingBlockParam/builder)
                            (.data ^String (:data blk))
                            (.build)))
    :tool-use (let [b (-> (BetaToolUseBlockParam/builder)
                           (.id ^String (:id blk))
                           (.name ^String (:name blk))
                           (.input ^BetaToolUseBlockParam$Input (->tool-input (:input blk))))]
                (when cache-control (.cacheControl b (->cache-control cache-control)))
                (BetaContentBlockParam/ofToolUse (.build b)))
    :tool-result (let [b (-> (BetaToolResultBlockParam/builder)
                              (.toolUseId ^String (:tool-use-id blk)))]
                   (if (string? (:content blk))
                     (.content b ^String (:content blk))
                     (.contentAsJson b (walk/stringify-keys (:content blk))))
                   (when (contains? blk :is-error) (.isError b (boolean (:is-error blk))))
                   (when cache-control (.cacheControl b (->cache-control cache-control)))
                   (BetaContentBlockParam/ofToolResult (.build b)))
    :tool-addition (let [^BetaRequestToolAdditionBlock$Builder b
                         (add-tool-change (BetaRequestToolAdditionBlock/builder)
                                          (:tool blk))]
                     (when cache-control (.cacheControl b (->cache-control cache-control)))
                     (BetaContentBlockParam/ofToolAddition (.build b)))
    :tool-removal (let [^BetaRequestToolRemovalBlock$Builder b
                        (remove-tool-change (BetaRequestToolRemovalBlock/builder)
                                            (:tool blk))]
                    (when cache-control (.cacheControl b (->cache-control cache-control)))
                    (BetaContentBlockParam/ofToolRemoval (.build b)))
    :compaction
    (let [b (com.anthropic.models.beta.messages.BetaCompactionBlockParam/builder)]
      (when-let [content (:content blk)] (.content b ^String content))
      (when-let [encrypted-content (:encrypted-content blk)]
        (.encryptedContent b ^String encrypted-content))
      (when-let [signature (:signature blk)] (.signature b ^String signature))
      (when cache-control (.cacheControl b (->cache-control cache-control)))
      (BetaContentBlockParam/ofCompaction (.build b)))
    (throw (ex-info "Unsupported beta content block type"
                    {:anthropic/error :unsupported-content-block :type type}))))

(defn- ->enum-value [value allowed constructor key]
  (let [k (keyword value)]
    (if (contains? allowed k)
      (constructor (-> k name (str/replace "-" "_") str/upper-case))
      (throw (ex-info (str "Unsupported " key " value")
                      {:anthropic/error :invalid-enum-value :key key :value value
                       :allowed allowed})))))

(defn- ->thinking-block-binding
  ^com.anthropic.models.beta.messages.BetaThinkingBlockBinding
  [{:keys [prefix-mismatch-behavior]}]
  (let [^com.anthropic.models.beta.messages.BetaThinkingBlockBinding$Builder b
        (com.anthropic.models.beta.messages.BetaThinkingBlockBinding/builder)]
    (when prefix-mismatch-behavior
      (let [^com.anthropic.models.beta.messages.BetaThinkingPrefixMismatchBehavior value
            (->enum-value prefix-mismatch-behavior #{:error :drop-block}
                          (fn [s#] (com.anthropic.models.beta.messages.BetaThinkingPrefixMismatchBehavior/of s#))
                          :prefix-mismatch-behavior)]
        (.prefixMismatchBehavior b value)))
    (.build b)))

(defn- ->thinking-enabled ^BetaThinkingConfigEnabled [{:keys [budget-tokens block-binding]}]
  (cond-> (BetaThinkingConfigEnabled/builder)
    true (.budgetTokens (long budget-tokens))
    block-binding (.blockBinding (->thinking-block-binding block-binding))
    true (.build)))

(defn- ->thinking-adaptive ^BetaThinkingConfigAdaptive [{:keys [block-binding]}]
  (cond-> (BetaThinkingConfigAdaptive/builder)
    block-binding (.blockBinding (->thinking-block-binding block-binding))
    true (.build)))

(defn- ->thinking ^BetaThinkingConfigParam [{:keys [type] :as thinking}]
  (case (keyword type)
    :enabled (BetaThinkingConfigParam/ofEnabled (->thinking-enabled thinking))
    :disabled (BetaThinkingConfigParam/ofDisabled (.build (BetaThinkingConfigDisabled/builder)))
    :adaptive (BetaThinkingConfigParam/ofAdaptive (->thinking-adaptive thinking))
    (throw (ex-info "Unsupported thinking type"
                    {:anthropic/error :unsupported-thinking-type :type type}))))

(defn- ->system-message-output-config
  ^com.anthropic.models.beta.messages.BetaSystemMessageOutputConfig
  [{:keys [effort]}]
  (let [^com.anthropic.models.beta.messages.BetaSystemMessageOutputConfig$Builder b
        (com.anthropic.models.beta.messages.BetaSystemMessageOutputConfig/builder)]
    (when effort
      (let [^com.anthropic.models.beta.messages.BetaSystemMessageOutputConfig$Effort value
            (->enum-value effort #{:low :medium :high :xhigh :max}
                          (fn [s#] (com.anthropic.models.beta.messages.BetaSystemMessageOutputConfig$Effort/of s#))
                          :effort)]
        (.effort b value)))
    (.build b)))

(defn- ->tool-choice ^BetaToolChoice [tc]
  (if (map? tc)
    (case (keyword (:type tc))
      :auto (let [b (BetaToolChoiceAuto/builder)]
              (when (contains? tc :disable-parallel-tool-use)
                (.disableParallelToolUse b (boolean (:disable-parallel-tool-use tc))))
              (BetaToolChoice/ofAuto (.build b)))
      :any (let [b (BetaToolChoiceAny/builder)]
             (when (contains? tc :disable-parallel-tool-use)
               (.disableParallelToolUse b (boolean (:disable-parallel-tool-use tc))))
             (BetaToolChoice/ofAny (.build b)))
      :none (if (contains? tc :disable-parallel-tool-use)
              ;; The API has no parallel tool use to disable when no tool runs.
              (throw (ex-info "Tool choice :none has no parallel tool use to disable"
                              {:anthropic/error :unsupported-disable-parallel-tool-use
                               :tool-choice tc}))
              (BetaToolChoice/ofNone (.build (BetaToolChoiceNone/builder))))
      (let [b (BetaToolChoiceTool/builder)]
        (.name b ^String (:name tc))
        (when (contains? tc :disable-parallel-tool-use)
          (.disableParallelToolUse b (boolean (:disable-parallel-tool-use tc))))
        (BetaToolChoice/ofTool (.build b))))
    (case (keyword tc)
      :auto (BetaToolChoice/ofAuto (.build (BetaToolChoiceAuto/builder)))
      :any (BetaToolChoice/ofAny (.build (BetaToolChoiceAny/builder)))
      :none (BetaToolChoice/ofNone (.build (BetaToolChoiceNone/builder)))
      (throw (ex-info "Unsupported tool choice"
                      {:anthropic/error :unsupported-tool-choice :tool-choice tc})))))

(defn- configure-tool-builder
  [{:keys [allowed-callers cache-control defer-loading strict]}
   {:keys [add-allowed-caller cache-control! defer-loading! strict!]}]
  (doseq [c allowed-callers]
    (add-allowed-caller c))
  (when cache-control (cache-control! (->cache-control cache-control)))
  (when (some? defer-loading) (defer-loading! defer-loading))
  (when (some? strict) (strict! strict)))

(defn- ->beta-bash-input-example [example]
  (let [b (BetaToolBash20250124$InputExample$Builder.)]
    (doseq [[k v] example] (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->beta-text-editor-input-example [example]
  (let [b (BetaToolTextEditor20250728$InputExample$Builder.)]
    (doseq [[k v] example] (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->beta-memory-input-example [example]
  (let [b (BetaMemoryTool20250818$InputExample$Builder.)]
    (doseq [[k v] example] (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->beta-computer-use-input-example [example]
  (let [b (BetaToolComputerUse20251124$InputExample$Builder.)]
    (doseq [[k v] example] (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->beta-custom-input-example [example]
  (let [b (BetaTool$InputExample$Builder.)]
    (doseq [[k v] example] (.putAdditionalProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->custom-tool ^BetaTool [{:keys [name description input-schema] :as t}]
  (let [schema (or input-schema {})
        properties (BetaTool$InputSchema$Properties/builder)
        schema-builder (-> (BetaTool$InputSchema/builder)
                           (.type (->json (or (:type schema) "object"))))
        b (-> (BetaTool/builder)
              (.name ^String name))]
    (doseq [[k v] (:properties schema)]
      ;; The tool's own `name` shadows `clojure.core/name` in this scope.
      (.putAdditionalProperty properties ^String (clojure.core/name k) (->json v)))
    (when (contains? schema :properties)
      (.properties schema-builder (.build properties)))
    (when (seq (:required schema)) (.required schema-builder ^java.util.List (vec (:required schema))))
    (.inputSchema b (.build schema-builder))
    (when description (.description b ^String description))
    (when (some? (:eager-input-streaming t))
      (.eagerInputStreaming b (boolean (:eager-input-streaming t))))
    (when (seq (:input-examples t))
      (.inputExamples b ^java.util.List (mapv ->beta-custom-input-example (:input-examples t))))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaTool$Builder b
                                               (BetaTool$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaTool$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaTool$Builder b (boolean %))
      :strict! #(.strict ^BetaTool$Builder b (boolean %))})
    (.build b)))

(defn- ->user-location ^BetaUserLocation [{:keys [city region country timezone]}]
  (let [b (BetaUserLocation/builder)]
    (when city (.city b ^String city))
    (when region (.region b ^String region))
    (when country (.country b ^String country))
    (when timezone (.timezone b ^String timezone))
    (.build b)))

(def ^:private server-tool-types
  #{:web-search :web-fetch :code-execution :bash :text-editor :memory
    :tool-search :computer-use :advisor :mcp-toolset :browser-toolset
    :computer-toolset})

(defn- ->web-search-tool ^BetaWebSearchTool20260318
  [{:keys [max-uses allowed-domains blocked-domains user-location response-inclusion] :as t}]
  (validate-allowed-domains! t)
  (let [b (BetaWebSearchTool20260318/builder)]
    (when max-uses (.maxUses b (long max-uses)))
    (when (seq allowed-domains) (.allowedDomains b ^java.util.List (vec allowed-domains)))
    (when (seq blocked-domains) (.blockedDomains b ^java.util.List (vec blocked-domains)))
    (when user-location (.userLocation b (->user-location user-location)))
    (when response-inclusion
      (.responseInclusion b (BetaWebSearchTool20260318$ResponseInclusion/of (name response-inclusion))))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaWebSearchTool20260318$Builder b
                                               (BetaWebSearchTool20260318$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaWebSearchTool20260318$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaWebSearchTool20260318$Builder b (boolean %))
      :strict! #(.strict ^BetaWebSearchTool20260318$Builder b (boolean %))})
    (.build b)))

(defn- ->citations ^BetaCitationsConfigParam [citations]
  (let [b (BetaCitationsConfigParam/builder)]
    (.enabled b (boolean (if (map? citations) (:enabled citations) citations)))
    (.build b)))

(defn- ->web-fetch-url-source-tools [tools]
  (mapv (fn [{:keys [type name]}]
          (case (keyword type)
            :tool-reference
            (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceToolReference/of
             ^String name)
            (throw (ex-info "Unsupported beta web-fetch URL source tool type"
                            {:anthropic/error :unsupported-web-fetch-url-source-tool
                             :type type}))))
        tools))

(defn- ->web-fetch-url-sources
  ^com.anthropic.models.beta.messages.BetaWebFetchUrlSources
  [{:keys [user-input client-tool-results server-tool-results]}]
  (let [b (com.anthropic.models.beta.messages.BetaWebFetchUrlSources/builder)
        set-source!
        (fn [scope {:keys [type tools]}]
          (let [type (keyword type)]
            (case [scope type]
              [:user-input :all]
              (.userInput b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceAll/builder)))
              [:user-input :none]
              (.userInput b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceNone/builder)))
              [:client-tool-results :all]
              (.clientToolResults b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceAll/builder)))
              [:client-tool-results :none]
              (.clientToolResults b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceNone/builder)))
              [:client-tool-results :only]
              (.onlyClientToolResults b ^java.util.List (->web-fetch-url-source-tools tools))
              [:client-tool-results :except]
              (.exceptClientToolResults b ^java.util.List (->web-fetch-url-source-tools tools))
              [:server-tool-results :all]
              (.serverToolResults b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceAll/builder)))
              [:server-tool-results :none]
              (.serverToolResults b (.build (com.anthropic.models.beta.messages.BetaWebFetchUrlSourceNone/builder)))
              [:server-tool-results :only]
              (.onlyServerToolResults b ^java.util.List (->web-fetch-url-source-tools tools))
              [:server-tool-results :except]
              (.exceptServerToolResults b ^java.util.List (->web-fetch-url-source-tools tools))
              (throw (ex-info "Unsupported beta web-fetch URL source"
                              {:anthropic/error :unsupported-web-fetch-url-source
                               :scope scope :type type})))))]
    (when user-input (set-source! :user-input user-input))
    (when client-tool-results (set-source! :client-tool-results client-tool-results))
    (when server-tool-results (set-source! :server-tool-results server-tool-results))
    (.build b)))

(defn- ->web-fetch-tool ^BetaWebFetchTool20260318
  [{:keys [max-uses max-content-tokens allowed-domains blocked-domains use-cache citations response-inclusion url-sources] :as t}]
  (validate-allowed-domains! t)
  (let [b (BetaWebFetchTool20260318/builder)]
    (when max-uses (.maxUses b (long max-uses)))
    (when max-content-tokens (.maxContentTokens b (long max-content-tokens)))
    (when (seq allowed-domains) (.allowedDomains b ^java.util.List (vec allowed-domains)))
    (when (seq blocked-domains) (.blockedDomains b ^java.util.List (vec blocked-domains)))
    (when (some? use-cache) (.useCache b (boolean use-cache)))
    (when citations (.citations b (->citations citations)))
    (when response-inclusion
      (.responseInclusion b (BetaWebFetchTool20260318$ResponseInclusion/of (name response-inclusion))))
    (when url-sources (.urlSources b (->web-fetch-url-sources url-sources)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaWebFetchTool20260318$Builder b
                                               (BetaWebFetchTool20260318$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaWebFetchTool20260318$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaWebFetchTool20260318$Builder b (boolean %))
      :strict! #(.strict ^BetaWebFetchTool20260318$Builder b (boolean %))})
    (.build b)))

(defn- ->code-execution-tool ^BetaCodeExecutionTool20260521 [t]
  (let [b (BetaCodeExecutionTool20260521/builder)]
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaCodeExecutionTool20260521$Builder b
                                               (BetaCodeExecutionTool20260521$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaCodeExecutionTool20260521$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaCodeExecutionTool20260521$Builder b (boolean %))
      :strict! #(.strict ^BetaCodeExecutionTool20260521$Builder b (boolean %))})
    (.build b)))

(defn- ->bash-tool ^BetaToolBash20250124 [{:keys [input-examples] :as t}]
  (let [b (BetaToolBash20250124/builder)]
    (when (seq input-examples)
      (.inputExamples b ^java.util.List (mapv ->beta-bash-input-example input-examples)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaToolBash20250124$Builder b
                                               (BetaToolBash20250124$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaToolBash20250124$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaToolBash20250124$Builder b (boolean %))
      :strict! #(.strict ^BetaToolBash20250124$Builder b (boolean %))})
    (.build b)))

(defn- ->text-editor-tool ^BetaToolTextEditor20250728
  [{:keys [max-characters input-examples] :as t}]
  (let [b (BetaToolTextEditor20250728/builder)]
    (when max-characters (.maxCharacters b (long max-characters)))
    (when (seq input-examples)
      (.inputExamples b ^java.util.List (mapv ->beta-text-editor-input-example input-examples)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaToolTextEditor20250728$Builder b
                                               (BetaToolTextEditor20250728$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaToolTextEditor20250728$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaToolTextEditor20250728$Builder b (boolean %))
      :strict! #(.strict ^BetaToolTextEditor20250728$Builder b (boolean %))})
    (.build b)))

(defn- ->memory-tool ^BetaMemoryTool20250818 [{:keys [input-examples] :as t}]
  (let [b (BetaMemoryTool20250818/builder)]
    (when (seq input-examples)
      (.inputExamples b ^java.util.List (mapv ->beta-memory-input-example input-examples)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaMemoryTool20250818$Builder b
                                               (BetaMemoryTool20250818$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaMemoryTool20250818$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaMemoryTool20250818$Builder b (boolean %))
      :strict! #(.strict ^BetaMemoryTool20250818$Builder b (boolean %))})
    (.build b)))

(defn- ->tool-search-bm25 ^BetaToolSearchToolBm25_20251119
  [{:keys [allowed-callers cache-control defer-loading strict]}]
  (let [b (BetaToolSearchToolBm25_20251119/builder)]
    (.type b BetaToolSearchToolBm25_20251119$Type/TOOL_SEARCH_TOOL_BM25_20251119)
    (doseq [c allowed-callers] (.addAllowedCaller b (BetaToolSearchToolBm25_20251119$AllowedCaller/of (clojure.core/name c))))
    (when cache-control (.cacheControl b ^BetaCacheControlEphemeral (->cache-control cache-control)))
    (when (some? defer-loading) (.deferLoading b (boolean defer-loading)))
    (when (some? strict) (.strict b (boolean strict)))
    (.build b)))

(defn- ->tool-search-regex ^BetaToolSearchToolRegex20251119
  [{:keys [allowed-callers cache-control defer-loading strict]}]
  (let [b (BetaToolSearchToolRegex20251119/builder)]
    (.type b BetaToolSearchToolRegex20251119$Type/TOOL_SEARCH_TOOL_REGEX_20251119)
    (doseq [c allowed-callers] (.addAllowedCaller b (BetaToolSearchToolRegex20251119$AllowedCaller/of (clojure.core/name c))))
    (when cache-control (.cacheControl b ^BetaCacheControlEphemeral (->cache-control cache-control)))
    (when (some? defer-loading) (.deferLoading b (boolean defer-loading)))
    (when (some? strict) (.strict b (boolean strict)))
    (.build b)))

(defn- ->computer-use-tool ^BetaToolComputerUse20251124
  [{:keys [display-height-px display-width-px display-number enable-zoom input-examples] :as t}]
  (let [b (BetaToolComputerUse20251124/builder)]
    (when display-height-px (.displayHeightPx b (long display-height-px)))
    (when display-width-px (.displayWidthPx b (long display-width-px)))
    (when display-number (.displayNumber b (long display-number)))
    (when (some? enable-zoom) (.enableZoom b (boolean enable-zoom)))
    (when (seq input-examples)
      (.inputExamples b ^java.util.List (mapv ->beta-computer-use-input-example input-examples)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaToolComputerUse20251124$Builder b
                                               (BetaToolComputerUse20251124$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaToolComputerUse20251124$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaToolComputerUse20251124$Builder b (boolean %))
      :strict! #(.strict ^BetaToolComputerUse20251124$Builder b (boolean %))})
    (.build b)))

(defn- ->advisor-tool ^BetaAdvisorTool20260301
  [{:keys [model max-tokens max-uses caching] :as t}]
  (let [b (BetaAdvisorTool20260301/builder)]
    (when model (.model b ^String model))
    (when max-tokens (.maxTokens b (long max-tokens)))
    (when max-uses (.maxUses b (long max-uses)))
    (when caching (.caching b ^BetaCacheControlEphemeral (->cache-control caching)))
    (configure-tool-builder
     t
     {:add-allowed-caller #(.addAllowedCaller ^BetaAdvisorTool20260301$Builder b
                                               (BetaAdvisorTool20260301$AllowedCaller/of (clojure.core/name %)))
      :cache-control! #(.cacheControl ^BetaAdvisorTool20260301$Builder b ^BetaCacheControlEphemeral %)
      :defer-loading! #(.deferLoading ^BetaAdvisorTool20260301$Builder b (boolean %))
      :strict! #(.strict ^BetaAdvisorTool20260301$Builder b (boolean %))})
    (.build b)))

(defn- ->mcp-toolset ^BetaMcpToolset
  [{:keys [mcp-server-name configs default-config cache-control]}]
  (let [b (BetaMcpToolset/builder)]
    (when mcp-server-name (.mcpServerName b ^String mcp-server-name))
    (when configs
      (let [cb (BetaMcpToolset$Configs/builder)]
        (doseq [[k v] configs] (.putAdditionalProperty cb (name k) (->json v)))
        (.configs b (.build cb))))
    (when default-config
      (let [db (BetaMcpToolDefaultConfig/builder)]
        (when (contains? default-config :defer-loading) (.deferLoading db (boolean (:defer-loading default-config))))
        (when (contains? default-config :enabled) (.enabled db (boolean (:enabled default-config))))
        (.defaultConfig b (.build db))))
    (when cache-control (.cacheControl b (->cache-control cache-control)))
    (.build b)))

(defmacro ^:private toolset-config-setters [builder-class entries]
  `(hash-map
    ~@(mapcat (fn [[action setter config-class]]
                (let [builder (with-meta (gensym "builder") {:tag builder-class})
                      config (gensym "config")
                      config-builder (gensym "config-builder")]
                  [action
                   `(fn [~builder ~config]
                      (let [~config-builder (~(symbol (str config-class "/builder")))]
                        (when (contains? ~config :enabled)
                          (.enabled ~config-builder (boolean (:enabled ~config))))
                        (when (contains? ~config :defer-loading)
                          (.deferLoading ~config-builder (boolean (:defer-loading ~config))))
                        (~(symbol (str "." setter)) ~builder (.build ~config-builder))))]))
              entries)))

(def ^:private browser-toolset-config-setters
  ;; The middle column is a Java method name consumed as data by the macro
  ;; (constructed into a compile-time interop call); clj-kondo reads it as an
  ;; unresolved var, so ignore that linter for this generated table.
  #_{:clj-kondo/ignore [:unresolved-symbol]}
  (toolset-config-setters BetaBrowserToolsetConfigs$Builder
    [[:close-tab closeTab BetaBrowserCloseTabConfig]
     [:double-click doubleClick BetaBrowserDoubleClickConfig]
     [:file-upload fileUpload BetaBrowserFileUploadConfig]
     [:find find BetaBrowserFindConfig]
     [:form-input formInput BetaBrowserFormInputConfig]
     [:get-page-text getPageText BetaBrowserGetPageTextConfig]
     [:hold-key holdKey BetaBrowserHoldKeyConfig]
     [:hover hover BetaBrowserHoverConfig]
     [:javascript-exec javascriptExec BetaBrowserJavascriptExecConfig]
     [:key key BetaBrowserKeyConfig]
     [:left-click leftClick BetaBrowserLeftClickConfig]
     [:left-click-drag leftClickDrag BetaBrowserLeftClickDragConfig]
     [:left-mouse-down leftMouseDown BetaBrowserLeftMouseDownConfig]
     [:left-mouse-up leftMouseUp BetaBrowserLeftMouseUpConfig]
     [:list-tabs listTabs BetaBrowserListTabsConfig]
     [:middle-click middleClick BetaBrowserMiddleClickConfig]
     [:mouse-move mouseMove BetaBrowserMouseMoveConfig]
     [:navigate navigate BetaBrowserNavigateConfig]
     [:new-tab newTab BetaBrowserNewTabConfig]
     [:read-console readConsole BetaBrowserReadConsoleConfig]
     [:read-network readNetwork BetaBrowserReadNetworkConfig]
     [:read-page readPage BetaBrowserReadPageConfig]
     [:right-click rightClick BetaBrowserRightClickConfig]
     [:screenshot screenshot BetaBrowserScreenshotConfig]
     [:scroll scroll BetaBrowserScrollConfig]
     [:scroll-to scrollTo BetaBrowserScrollToConfig]
     [:switch-tab switchTab BetaBrowserSwitchTabConfig]
     [:triple-click tripleClick BetaBrowserTripleClickConfig]
     [:type type BetaBrowserTypeConfig]
     [:wait wait BetaBrowserWaitConfig]
     [:zoom zoom BetaBrowserZoomConfig]]))

(def ^:private computer-toolset-config-setters
  ;; See note on browser-toolset-config-setters: the method-name column is data.
  #_{:clj-kondo/ignore [:unresolved-symbol]}
  (toolset-config-setters BetaComputerToolsetConfigs$Builder
    [[:cursor-position cursorPosition BetaComputerCursorPositionConfig]
     [:double-click doubleClick BetaComputerDoubleClickConfig]
     [:hold-key holdKey BetaComputerHoldKeyConfig]
     [:key key BetaComputerKeyConfig]
     [:left-click leftClick BetaComputerLeftClickConfig]
     [:left-click-drag leftClickDrag BetaComputerLeftClickDragConfig]
     [:left-mouse-down leftMouseDown BetaComputerLeftMouseDownConfig]
     [:left-mouse-up leftMouseUp BetaComputerLeftMouseUpConfig]
     [:middle-click middleClick BetaComputerMiddleClickConfig]
     [:mouse-move mouseMove BetaComputerMouseMoveConfig]
     [:right-click rightClick BetaComputerRightClickConfig]
     [:screenshot screenshot BetaComputerScreenshotConfig]
     [:scroll scroll BetaComputerScrollConfig]
     [:triple-click tripleClick BetaComputerTripleClickConfig]
     [:type type BetaComputerTypeConfig]
     [:wait wait BetaComputerWaitConfig]
     [:zoom zoom BetaComputerZoomConfig]]))

(defn- ->browser-toolset-configs ^BetaBrowserToolsetConfigs [configs]
  (let [b (BetaBrowserToolsetConfigs/builder)]
    (doseq [[action config] configs]
      (when-let [set-config! (get browser-toolset-config-setters action)]
        (set-config! b config)))
    (.build b)))

(defn- ->computer-toolset-configs ^BetaComputerToolsetConfigs [configs]
  (let [b (BetaComputerToolsetConfigs/builder)]
    (doseq [[action config] configs]
      (when-let [set-config! (get computer-toolset-config-setters action)]
        (set-config! b config)))
    (.build b)))

(defn- ->browser-toolset ^BetaBrowserToolset20260801 [{:keys [configs cache-control]}]
  (let [b (BetaBrowserToolset20260801/builder)]
    (when (seq configs) (.configs b (->browser-toolset-configs configs)))
    (when cache-control (.cacheControl b (->cache-control cache-control)))
    (.build b)))

(defn- ->computer-toolset ^BetaComputerToolset20260801 [{:keys [configs cache-control]}]
  (let [b (BetaComputerToolset20260801/builder)]
    (when (seq configs) (.configs b (->computer-toolset-configs configs)))
    (when cache-control (.cacheControl b (->cache-control cache-control)))
    (.build b)))

(def ^:private dated-tool-variants
  {:web-search {"20250305" ["BetaWebSearchTool20250305" "ofWebSearchTool20250305" "ofBetaWebSearchTool20250305"]
                "20260209" ["BetaWebSearchTool20260209" "ofWebSearchTool20260209" "ofBetaWebSearchTool20260209"]
                "20260318" ["BetaWebSearchTool20260318" "ofWebSearchTool20260318" "ofBetaWebSearchTool20260318"]}
   :web-fetch {"20250910" ["BetaWebFetchTool20250910" "ofWebFetchTool20250910" "ofBetaWebFetchTool20250910"]
               "20260209" ["BetaWebFetchTool20260209" "ofWebFetchTool20260209" "ofBetaWebFetchTool20260209"]
               "20260309" ["BetaWebFetchTool20260309" "ofWebFetchTool20260309" "ofBetaWebFetchTool20260309"]
               "20260318" ["BetaWebFetchTool20260318" "ofWebFetchTool20260318" "ofBetaWebFetchTool20260318"]}
   :code-execution {"20250522" ["BetaCodeExecutionTool20250522" "ofCodeExecutionTool20250522" "ofBetaCodeExecutionTool20250522"]
                    "20250825" ["BetaCodeExecutionTool20250825" "ofCodeExecutionTool20250825" "ofBetaCodeExecutionTool20250825"]
                    "20260120" ["BetaCodeExecutionTool20260120" "ofCodeExecutionTool20260120" "ofBetaCodeExecutionTool20260120"]
                    "20260521" ["BetaCodeExecutionTool20260521" "ofCodeExecutionTool20260521" "ofBetaCodeExecutionTool20260521"]}
   :bash {"20241022" ["BetaToolBash20241022" "ofBash20241022" "ofBetaToolBash20241022"]
          "20250124" ["BetaToolBash20250124" "ofBash20250124" "ofBetaToolBash20250124"]}
   :text-editor {"20241022" ["BetaToolTextEditor20241022" "ofTextEditor20241022" "ofBetaToolTextEditor20241022"]
                 "20250124" ["BetaToolTextEditor20250124" "ofTextEditor20250124" "ofBetaToolTextEditor20250124"]
                 "20250429" ["BetaToolTextEditor20250429" "ofTextEditor20250429" "ofBetaToolTextEditor20250429"]
                 "20250728" ["BetaToolTextEditor20250728" "ofTextEditor20250728" "ofBetaToolTextEditor20250728"]}
   :computer-use {"20241022" ["BetaToolComputerUse20241022" "ofComputerUse20241022" "ofBetaToolComputerUse20241022"]
                  "20250124" ["BetaToolComputerUse20250124" "ofComputerUse20250124" "ofBetaToolComputerUse20250124"]
                  "20251124" ["BetaToolComputerUse20251124" "ofComputerUse20251124" "ofBetaToolComputerUse20251124"]}})

(defn- invoke-method [^Object target method & values]
  (let [^Class target-class (class target)
        compatible? (fn [^Class parameter-class value]
                     (let [value-class (class value)]
                       (or (and value-class (.isAssignableFrom parameter-class value-class))
                           (and (.isPrimitive parameter-class)
                                (= value-class
                                   ({Boolean/TYPE Boolean
                                     Long/TYPE Long
                                     Integer/TYPE Integer
                                     Double/TYPE Double}
                                    parameter-class))))))
        ^java.lang.reflect.Method method-ref
        (first (filter #(and (= method (.getName ^java.lang.reflect.Method %))
                             (= (count values) (alength (.getParameterTypes ^java.lang.reflect.Method %)))
                             (every? true?
                                     (map compatible?
                                          (vec (.getParameterTypes ^java.lang.reflect.Method %))
                                          values)))
                       (.getMethods target-class)))]
    (.invoke method-ref target (object-array values))))

(defn- invoke-static-zero [class-name method]
  (let [^Class target-class (Class/forName (str "com.anthropic.models.beta.messages." class-name))
        ^java.lang.reflect.Method method-ref
        (first (filter #(and (= method (.getName ^java.lang.reflect.Method %))
                             (= 0 (alength (.getParameterTypes ^java.lang.reflect.Method %))))
                       (.getMethods target-class)))]
    (.invoke method-ref nil (object-array 0))))

(defn- new-instance [class-name]
  (let [^Class target-class (Class/forName (str "com.anthropic.models.beta.messages." class-name))]
    (.newInstance target-class)))

(defn- invoke-static [class-name method value]
  (let [^Class target-class (Class/forName (str "com.anthropic.models.beta.messages." class-name))
        ^java.lang.reflect.Method method-ref
        (first (filter #(and (= method (.getName ^java.lang.reflect.Method %))
                             (= 1 (alength (.getParameterTypes ^java.lang.reflect.Method %))))
                       (.getMethods target-class)))]
    (.invoke method-ref nil (object-array [value]))))

(defn- build-dated-tool [class-name t]
  (when (contains? t :allowed-domains)
    (validate-allowed-domains! t))
  (let [builder (invoke-static-zero class-name "builder")
        input-class (str class-name "$InputExample$Builder")]
    (doseq [c (:allowed-callers t)] (invoke-method builder "addAllowedCaller" (invoke-static (str class-name "$AllowedCaller") "of" (name c))))
    (when (:cache-control t) (invoke-method builder "cacheControl" (->cache-control (:cache-control t))))
    (when (some? (:defer-loading t)) (invoke-method builder "deferLoading" (boolean (:defer-loading t))))
    (when (some? (:strict t)) (invoke-method builder "strict" (boolean (:strict t))))
    (when-let [v (:max-uses t)] (invoke-method builder "maxUses" (long v)))
    (when-let [v (:max-content-tokens t)] (invoke-method builder "maxContentTokens" (long v)))
    (when-let [v (:max-characters t)] (invoke-method builder "maxCharacters" (long v)))
    (when-let [v (:display-height-px t)] (invoke-method builder "displayHeightPx" (long v)))
    (when-let [v (:display-width-px t)] (invoke-method builder "displayWidthPx" (long v)))
    (when-let [v (:display-number t)] (invoke-method builder "displayNumber" (long v)))
    (when (some? (:enable-zoom t)) (invoke-method builder "enableZoom" (boolean (:enable-zoom t))))
    (when (some? (:use-cache t)) (invoke-method builder "useCache" (boolean (:use-cache t))))
    (when-let [v (:model t)] (invoke-method builder "model" ^String v))
    (when-let [v (:max-tokens t)] (invoke-method builder "maxTokens" (long v)))
    (when-let [v (:caching t)] (invoke-method builder "caching" (->cache-control v)))
    (when (:user-location t) (invoke-method builder "userLocation" (->user-location (:user-location t))))
    (when (:citations t) (invoke-method builder "citations" (->citations (:citations t))))
    (when-let [v (:response-inclusion t)]
      (when-let [response-class (try (Class/forName (str "com.anthropic.models.beta.messages." class-name "$ResponseInclusion")) (catch ClassNotFoundException _ nil))]
        (when-let [response-method (first (filter #(= "of" (.getName ^java.lang.reflect.Method %)) (.getMethods ^Class response-class)))]
          (invoke-method builder "responseInclusion"
                         (.invoke ^java.lang.reflect.Method response-method nil (object-array [(name v)]))))))
    (when (str/starts-with? class-name "BetaWebFetchTool")
      (when-let [url-sources (:url-sources t)]
        (invoke-method builder "urlSources" (->web-fetch-url-sources url-sources))))
    (when (seq (:allowed-domains t)) (invoke-method builder "allowedDomains" ^java.util.List (vec (:allowed-domains t))))
    (when (seq (:blocked-domains t)) (invoke-method builder "blockedDomains" ^java.util.List (vec (:blocked-domains t))))
    (when (seq (:input-examples t))
      (invoke-method builder "inputExamples"
                     ^java.util.List
                     (mapv (fn [example]
                             (let [input-builder (new-instance input-class)]
                               (doseq [[k v] example]
                                 (invoke-method input-builder "putAdditionalProperty" (name k) (->json v)))
                               (invoke-method input-builder "build")))
                           (:input-examples t))))
    (invoke-method builder "build")))

(defn- dated-tool [family t]
  (let [variants (get dated-tool-variants family)
        version (if-let [version (:version t)] (if (keyword? version) (name version) (str version))
                      (last (sort (keys variants))))
        [class-name _ _] (get variants version)]
    (if class-name
      (build-dated-tool class-name t)
      (throw (ex-info "Unsupported server tool version"
                      {:anthropic/error :unsupported-server-tool-version
                       :type family :version (:version t)})))))

(defn- validate-tool-version [family t expected]
  (when-let [version (:version t)]
    (let [version (if (keyword? version) (name version) (str version))]
      (when-not (= expected version)
        (throw (ex-info "Unsupported server tool version"
                        {:anthropic/error :unsupported-server-tool-version
                         :type family :version (:version t)})))))
  t)

(defn- ->server-tool ^BetaToolUnion [{:keys [type] :as t}]
  (let [family (keyword type)]
    (case family
      (:web-search :web-fetch :code-execution :bash :text-editor :computer-use)
      (let [version (if-let [version (:version t)] (if (keyword? version) (name version) (str version))
                    (last (sort (keys (get dated-tool-variants family)))))
            [_ constructor _] (get-in dated-tool-variants [family version])]
        (if constructor
          (invoke-static "BetaToolUnion" constructor (dated-tool family t))
          (throw (ex-info "Unsupported server tool version"
                          {:anthropic/error :unsupported-server-tool-version
                           :type family :version (:version t)}))))
      :memory (invoke-static "BetaToolUnion" "ofMemoryTool20250818"
                             (->memory-tool (validate-tool-version family t "20250818")))
      :tool-search (case (keyword (:variant t))
                     :bm25 (BetaToolUnion/ofSearchToolBm25_20251119
                            (->tool-search-bm25 (validate-tool-version family t "20251119")))
                     :regex (BetaToolUnion/ofSearchToolRegex20251119
                             (->tool-search-regex (validate-tool-version family t "20251119")))
                     (throw (ex-info "Unsupported tool-search variant"
                                     {:anthropic/error :unsupported-tool-search-variant
                                      :variant (:variant t)})))
      :advisor (BetaToolUnion/ofAdvisorTool20260301
                (->advisor-tool (validate-tool-version family t "20260301")))
      :mcp-toolset (BetaToolUnion/ofMcpToolset (->mcp-toolset t))
      :browser-toolset (BetaToolUnion/ofBrowserToolset20260801 (->browser-toolset t))
      :computer-toolset (BetaToolUnion/ofComputerToolset20260801 (->computer-toolset t))
      (throw (ex-info "Unsupported server tool type" {:anthropic/error :unsupported-server-tool :type type})))))

(defn- ->count-tool ^MessageCountTokensParams$Tool [{:keys [type] :as t}]
  (let [family (keyword type)]
    (if (contains? dated-tool-variants family)
      (let [version (if-let [version (:version t)] (if (keyword? version) (name version) (str version))
                    (last (sort (keys (get dated-tool-variants family)))))
            [_ _ constructor] (get-in dated-tool-variants [family version])]
        (if constructor
          (invoke-static "MessageCountTokensParams$Tool" constructor (dated-tool family t))
          (throw (ex-info "Unsupported server tool version"
                          {:anthropic/error :unsupported-server-tool-version
                           :type family :version (:version t)}))))
      (case family
        :memory (invoke-static "MessageCountTokensParams$Tool" "ofBetaMemoryTool20250818"
                               (->memory-tool (validate-tool-version family t "20250818")))
        :tool-search (case (keyword (:variant t))
                        :bm25 (MessageCountTokensParams$Tool/ofBetaToolSearchToolBm25_20251119
                               (->tool-search-bm25 (validate-tool-version family t "20251119")))
                        :regex (MessageCountTokensParams$Tool/ofBetaToolSearchToolRegex20251119
                                (->tool-search-regex (validate-tool-version family t "20251119")))
                        (throw (ex-info "Unsupported tool-search variant"
                                        {:anthropic/error :unsupported-tool-search-variant
                                         :variant (:variant t)})))
        :advisor (MessageCountTokensParams$Tool/ofBetaAdvisorTool20260301
                  (->advisor-tool (validate-tool-version family t "20260301")))
        :mcp-toolset (MessageCountTokensParams$Tool/ofBetaMcpToolset (->mcp-toolset t))
        :browser-toolset (MessageCountTokensParams$Tool/ofBetaBrowserToolset20260801
                          (->browser-toolset t))
        :computer-toolset (MessageCountTokensParams$Tool/ofBetaComputerToolset20260801
                           (->computer-toolset t))
        (MessageCountTokensParams$Tool/ofBeta (->custom-tool t))))))

(defn- server-tool? [t]
  ;; Only a recognized `:type` makes a tool server-side. A tool carrying `:fn` is
  ;; executed locally by `run-beta-tools`, so it is always custom. Any other
  ;; `:type` belongs to the caller and must not steal the custom-tool path.
  (and (nil? (:fn t))
       (contains? server-tool-types (keyword (:type t)))))

(defn- ->tool ^BetaToolUnion [t]
  (if (server-tool? t)
    (->server-tool t)
    (BetaToolUnion/ofBetaTool (->custom-tool t))))

(defn- ->metadata ^BetaMetadata [{:keys [user-id]}]
  (-> (BetaMetadata/builder) (.userId ^String user-id) (.build)))

(defn- ->json-output-format ^BetaJsonOutputFormat [schema]
  (let [sb (BetaJsonOutputFormat$Schema/builder)]
    (doseq [[k v] schema]
      (.putAdditionalProperty sb ^String (name k) (->json v)))
    (-> (BetaJsonOutputFormat/builder) (.schema (.build sb)) (.build))))

(defn- ->output-config ^BetaOutputConfig [schema effort task-budget output-type]
  (if output-type
    (let [b (StructuredOutputConfig/builder)]
      (.format b ^Class output-type)
      (when effort (.effort b (BetaOutputConfig$Effort/of (name effort))))
      (.rawOutputConfig (.build b)))
    (let [b (BetaOutputConfig/builder)]
      (when schema (.format b (->json-output-format schema)))
      (when effort (.effort b (BetaOutputConfig$Effort/of (name effort))))
      (when task-budget
        (.taskBudget b
                     (let [tb (BetaTokenTaskBudget/builder)]
                       (.total tb (long (:total task-budget)))
                       (when (:remaining task-budget) (.remaining tb (long (:remaining task-budget))))
                       (.build tb))))
      (.build b))))

(defn- ->context-edit ^BetaContextManagementConfig$Edit
  [{:keys [type clear-tool-inputs instructions keep pause-after-compaction trigger] :as edit}]
  (case (keyword type)
    :clear-tool-uses-20250919
    (BetaContextManagementConfig$Edit/ofClearToolUses20250919
     (let [b (BetaClearToolUses20250919Edit/builder)]
       (when (contains? edit :clear-tool-inputs)
         (.clearToolInputs b (boolean clear-tool-inputs)))
       (.build b)))
    :clear-thinking-20251015
    (BetaContextManagementConfig$Edit/ofClearThinking20251015
     (let [b (BetaClearThinking20251015Edit/builder)]
       (when keep
         (if (= :all (keyword keep))
           (.keepAll b)))
       (.build b)))
    :compact-20260112
    (BetaContextManagementConfig$Edit/ofCompact20260112
     (let [b (BetaCompact20260112Edit/builder)]
       (when instructions (.instructions b ^String instructions))
       (when (contains? edit :pause-after-compaction)
         (.pauseAfterCompaction b (boolean pause-after-compaction)))
       (when trigger
         (.trigger b (BetaInputTokensTrigger/of (long (:input-tokens trigger)))))
       (.build b)))
    (throw (ex-info "Unsupported context management edit"
                    {:anthropic/error :unsupported-context-management-edit :type type}))))

(defn- ->context-management ^BetaContextManagementConfig
  [{:keys [edits]}]
  (let [b (BetaContextManagementConfig/builder)]
    (doseq [edit edits]
      (.addEdit b ^BetaContextManagementConfig$Edit (->context-edit edit)))
    (.build b)))

(defn- ->compaction
  ^com.anthropic.models.beta.messages.BetaCompactionConfig
  [{:keys [type instructions]}]
  (case (keyword type)
    :summarize (let [b (com.anthropic.models.beta.messages.BetaCompactionConfig/builder)]
                 (when instructions (.instructions b ^String instructions))
                 (.build b))
    (throw (ex-info "Unsupported compaction type"
                    {:anthropic/error :unsupported-compaction-type :type type}))))

(defn- ->diagnostics ^BetaDiagnosticsParam
  [{:keys [previous-message-id]}]
  (let [b (BetaDiagnosticsParam/builder)]
    (when previous-message-id (.previousMessageId b ^String previous-message-id))
    (.build b)))

(defn- ->fallback-param ^BetaFallbackParam
  [{:keys [model max-tokens output-config speed thinking]}]
  (let [b (doto (BetaFallbackParam/builder)
            (.model ^String model))]
    (when max-tokens (.maxTokens b (long max-tokens)))
    (when output-config
      (.outputConfig b (->output-config (:schema output-config) (:effort output-config) (:task-budget output-config) nil)))
    (when speed (.speed b (BetaFallbackParam$Speed/of (name speed))))
    (when thinking
      (case (keyword (:type thinking))
        :enabled (.thinking b (->thinking-enabled thinking))
        :disabled (.thinking b (.build (BetaThinkingConfigDisabled/builder)))
        :adaptive (.thinking b (->thinking-adaptive thinking))))
    (.build b)))

(defn- ->message-param
  ^BetaMessageParam
  [{:keys [role content clear-at output-config]}]
  (let [^BetaMessageParam$Builder b (BetaMessageParam/builder)]
    (.role b (BetaMessageParam$Role/of (name (keyword role))))
    (if (string? content)
      (.content b ^String content)
      (.contentOfBetaContentBlockParams b ^java.util.List (mapv ->content-block content)))
    (when clear-at
      (let [^BetaMessageParam$ClearAt value
            (->enum-value clear-at #{:never :next-user-message}
                          (fn [s#] (BetaMessageParam$ClearAt/of s#)) :clear-at)]
        (.clearAt b value)))
    (when output-config (.outputConfig b (->system-message-output-config output-config)))
    (.build b)))

(defn- ->mcp-server ^BetaRequestMcpServerUrlDefinition
  [{:keys [name url authorization-token tool-configuration]}]
  (let [b (-> (BetaRequestMcpServerUrlDefinition/builder)
              (.name ^String name)
              (.url ^String url))]
    (when authorization-token (.authorizationToken b ^String authorization-token))
    (when tool-configuration
      (let [tb (BetaRequestMcpServerToolConfiguration/builder)]
        (when-let [allowed-tools (:allowed-tools tool-configuration)]
          (.allowedTools tb ^java.util.List (vec allowed-tools)))
        (when (contains? tool-configuration :enabled)
          (.enabled tb (boolean (:enabled tool-configuration))))
        (.toolConfiguration b (.build tb))))
    (.build b)))

(defn- add-create-message [^MessageCreateParams$Builder b {:keys [role content] :as message}]
  (let [role (keyword role)]
    (if (or (contains? message :clear-at) (contains? message :output-config))
      (.addMessage b (->message-param message))
      (if (string? content)
      (case role
        :user (.addUserMessage b ^String content)
        :assistant (.addAssistantMessage b ^String content))
      (let [blocks (mapv ->content-block content)]
        (case role
          :user (.addUserMessageOfBetaContentBlockParams b ^java.util.List blocks)
          :assistant (.addAssistantMessageOfBetaContentBlockParams b ^java.util.List blocks)))))))

(defn- add-count-message [^MessageCountTokensParams$Builder b {:keys [role content] :as message}]
  (let [role (keyword role)]
    (if (or (contains? message :clear-at) (contains? message :output-config))
      (.addMessage b (->message-param message))
      (if (string? content)
      (case role
        :user (.addUserMessage b ^String content)
        :assistant (.addAssistantMessage b ^String content))
      (let [blocks (mapv ->content-block content)]
        (case role
          :user (.addUserMessageOfBetaContentBlockParams b ^java.util.List blocks)
          :assistant (.addAssistantMessageOfBetaContentBlockParams b ^java.util.List blocks)))))))

(defn- ->params ^MessageCreateParams
  [{:keys [model max-tokens system messages tools temperature top-p top-k stop-sequences
           tool-choice thinking metadata service-tier response-format output-format output-type effort container inference-geo
           task-budget
           compaction context-management diagnostics speed
           user-profile-id cache-control betas mcp-servers fallbacks fallback-credit-token
           extra-headers extra-query extra-body]
    :or {model "claude-opus-4-8" max-tokens 1024}}]
  (let [^String model-name (if (keyword? model) (name model) model)
        b (doto (MessageCreateParams/builder)
            (.model model-name)
            (.maxTokens (long max-tokens)))]
    (when system
      (if (string? system)
        (.system b ^String system)
        (.systemOfBetaTextBlockParams b ^java.util.List (mapv ->system-block system))))
    (when temperature (.temperature b (double temperature)))
    (when top-p (.topP b (double top-p)))
    (when top-k (.topK b (long top-k)))
    (when (seq stop-sequences) (.stopSequences b ^java.util.List (vec stop-sequences)))
    (when tool-choice (.toolChoice b (->tool-choice tool-choice)))
    (when thinking (.thinking b (->thinking thinking)))
    (when metadata (.metadata b (->metadata metadata)))
    (when service-tier (.serviceTier b (MessageCreateParams$ServiceTier/of (-> service-tier name (str/replace "-" "_")))))
    (when container
      (if (string? container)
        (.container b ^String container)
        (.container b ^BetaContainerParams (->beta-container-params container))))
    (when inference-geo (.inferenceGeo b ^String inference-geo))
    (when user-profile-id (.userProfileId b ^String user-profile-id))
    (when cache-control (.cacheControl b (->cache-control cache-control)))
    (when (or response-format output-type effort task-budget) (.outputConfig b (->output-config response-format effort task-budget output-type)))
    (when output-format (.outputFormat b (->json-output-format output-format)))
    (when compaction (.compaction b (->compaction compaction)))
    (when context-management (.contextManagement b (->context-management context-management)))
    (when diagnostics (.diagnostics b (->diagnostics diagnostics)))
    (when speed
      (.speed b (case (keyword speed)
                  :standard MessageCreateParams$Speed/STANDARD
                  :fast MessageCreateParams$Speed/FAST
                  (throw (ex-info "Unsupported speed"
                                  {:anthropic/error :unsupported-speed :speed speed})))))
    (when fallbacks
      (if (= :default (keyword fallbacks))
        (.fallbacksDefault b)
        (.fallbacksOfFallbackParams b (mapv ->fallback-param fallbacks))))
    (when fallback-credit-token (.fallbackCreditToken b ^String fallback-credit-token))
    (doseq [beta betas]
      (let [^String beta-name (if (keyword? beta) (name beta) beta)]
        (.addBeta b beta-name)))
    (doseq [tool tools] (.addTool b (->tool tool)))
    (doseq [server mcp-servers] (.addMcpServer b (->mcp-server server)))
    (doseq [message messages] (add-create-message b message))
    (doseq [[k v] extra-headers] (.putAdditionalHeader b ^String (name k) ^String v))
    (doseq [[k v] extra-query] (.putAdditionalQueryParam b ^String (name k) ^String v))
    (doseq [[k v] extra-body] (.putAdditionalBodyProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- ->count-params ^MessageCountTokensParams
  [{:keys [model system messages tools thinking tool-choice betas cache-control compaction context-management
           mcp-servers response-format output-type effort task-budget output-format speed user-profile-id
           extra-headers extra-query extra-body]
    :or {model "claude-opus-4-8"}}]
  (let [^String model-name (if (keyword? model) (name model) model)
        b (doto (MessageCountTokensParams/builder)
            (.model model-name))]
    (when system
      (if (string? system)
        (.system b ^String system)
        (.systemOfBetaTextBlockParams b ^java.util.List (mapv ->system-block system))))
    (when thinking (.thinking b (->thinking thinking)))
    (when tool-choice (.toolChoice b (->tool-choice tool-choice)))
    (doseq [beta betas]
      (let [^String beta-name (if (keyword? beta) (name beta) beta)]
        (.addBeta b beta-name)))
    (when cache-control (.cacheControl b (->cache-control cache-control)))
    (when user-profile-id (.userProfileId b ^String user-profile-id))
    (when (or response-format output-type effort task-budget) (.outputConfig b (->output-config response-format effort task-budget output-type)))
    (when output-format (.outputFormat b (->json-output-format output-format)))
    (when compaction (.compaction b (->compaction compaction)))
    (when context-management (.contextManagement b (->context-management context-management)))
    (when speed
      (.speed b (case (keyword speed)
                  :standard MessageCountTokensParams$Speed/STANDARD
                  :fast MessageCountTokensParams$Speed/FAST
                  (throw (ex-info "Unsupported speed"
                                  {:anthropic/error :unsupported-speed :speed speed})))))
    (doseq [server mcp-servers] (.addMcpServer b (->mcp-server server)))
    (doseq [tool tools] (.addTool b (->count-tool tool)))
    (doseq [message messages] (add-count-message b message))
    (doseq [[k v] extra-headers] (.putAdditionalHeader b ^String (name k) ^String v))
    (doseq [[k v] extra-query] (.putAdditionalQueryParam b ^String (name k) ^String v))
    (doseq [[k v] extra-body] (.putAdditionalBodyProperty b ^String (name k) (->json v)))
    (.build b)))

(defn- java->clj [x]
  (cond
    (instance? java.util.Map x) (persistent!
                                 (reduce-kv (fn [acc k v]
                                              (let [value (java->clj v)]
                                                (if (nil? value)
                                                  acc
                                                  (assoc! acc (keyword (str/replace (str k) "_" "-")) value))))
                                            (transient {}) (into {} x)))
    (instance? java.util.List x) (mapv java->clj x)
    :else x))

(defn- json->clj [^JsonValue jv]
  (java->clj (.convert jv java.lang.Object)))

(defn- ->keyword [x]
  (if (keyword? x)
    x
    (-> x str str/lower-case (str/replace #"[._]" "-") keyword)))

(defn- keywordize-types
  "Convert every nested `:type` string to a keyword. A block's `:type` reads the
   same here as on the stable path. Leave `:input` unchanged: a tool call's arguments
   are caller-defined JSON where a `type` key is data, not a discriminator."
  [x]
  (cond
    (map? x) (reduce-kv (fn [m k v]
                          (assoc m k (cond
                                       (= :input k) v
                                       (and (= :type k) (string? v)) (->keyword v)
                                       :else (keywordize-types v))))
                        {} x)
    (sequential? x) (mapv keywordize-types x)
    :else x))

(defn- keywordize-message-enums [m]
  (letfn [(convert [x]
            (if (map? x)
              (reduce-kv
               (fn [out k v]
                 (let [v (convert v)
                       v (cond
                           (and (= :input-transformations k) (sequential? v))
                           (mapv #(if (and (map? %) (contains? % :reason))
                                    (update % :reason ->keyword) %)
                                 v)
                           (and (= :output-config k) (map? v) (contains? v :effort))
                           (update v :effort ->keyword)
                           (and (= :block-binding k) (map? v)
                                (contains? v :prefix-mismatch-behavior))
                           (update v :prefix-mismatch-behavior ->keyword)
                           :else v)]
                   (assoc out k v))) {} x)
              (if (sequential? x) (mapv convert x) x)))]
    (convert m)))

(defn- beta-message->map [^BetaMessage message]
  (let [m (keywordize-message-enums (keywordize-types (json->clj (JsonValue/from message))))]
    (cond-> m
      (string? (:role m)) (update :role ->keyword)
      (string? (:stop-reason m)) (update :stop-reason ->keyword)
      (string? (:clear-at m)) (update :clear-at ->keyword))))

(defn- beta-tokens-count->map [^BetaMessageTokensCount result]
  (let [^java.util.Optional context-management (.contextManagement result)]
    (cond-> {:input-tokens (.inputTokens result)}
      (.isPresent context-management)
      (assoc :context-management
             (json->clj
              (JsonValue/from ^BetaCountTokensContextManagementResponse
                             (.get context-management)))))))

(defn- ->request-options ^RequestOptions [{:keys [timeout-ms response-validation] :as opts}]
  (if (or (contains? opts :timeout-ms) (contains? opts :response-validation))
    (let [b (RequestOptions/builder)]
      (when (contains? opts :timeout-ms)
        (.timeout b (java.time.Duration/ofMillis (long timeout-ms))))
      (when (contains? opts :response-validation)
        (.responseValidation b (boolean response-validation)))
      (.build b))
    (RequestOptions/none)))

(defn- headers->map [^Headers headers]
  (into {} (map (fn [^String name] [(str/lower-case name) (vec (.values headers name))])) (.names headers)))

(defn- response-metadata [^HttpResponse response]
  (let [request-id (.requestId response)]
    {:status (.statusCode response)
     :request-id (when (.isPresent request-id) (.get request-id))
     :headers (headers->map (.headers response))}))

(defn- parse-beta-text
  "Decode the first text block of a beta response map as JSON, or nil."
  [response]
  (when-let [text (->> (:content response)
                       (filter #(= :text (:type %)))
                       first
                       :text)]
    (json/read-value text json-mapper)))

(defn create-beta-message
  "Send a beta Messages request and return a generic Clojure map response.

  Request maps support compaction, context-management, diagnostics, speed, and
  tool-choice disable-parallel-tool-use options. Tool specs support response-inclusion,
  input-examples, eager-input-streaming, caching, and dated :version options.
  System text and text content blocks accept citation-list `:citations`; document
  content blocks accept boolean or `{:enabled ...}` citation configuration.
  Custom tools accept optional :description and :input-schema; an omitted schema
  defaults to the SDK's object schema."
  ([^AnthropicClient client req] (create-beta-message client req {}))
  ([^AnthropicClient client req opts]
   (with-api-errors
     (let [params (->params req)
           request-options (->request-options opts)
           response (if (:include-response opts)
                      (with-open [^HttpResponseFor raw-response (.create (.withRawResponse (.messages (.beta client))) params request-options)]
                        (assoc (beta-message->map (.parse raw-response))
                               :response (response-metadata raw-response)))
                      (beta-message->map (.create (.messages (.beta client)) params request-options)))]
       (cond-> response
         (:response-format req) (assoc :parsed (parse-beta-text response)))))))

(defn- strip-tool-fns [params]
  (if (contains? params :tools)
    (update params :tools #(mapv (fn [tool] (dissoc tool :fn)) %))
    params))

(defn- beta-tool-fns [tools]
  (into {}
        (keep (fn [{:keys [name fn]}]
                (when fn [name fn])))
        tools))

(defn- beta-tool-result [block f]
  (try
    {:type :tool-result
     :tool-use-id (:id block)
     :content (f (:input block))}
    (catch Throwable e
      {:type :tool-result
       :tool-use-id (:id block)
       :content (or (.getMessage e) (str e))
       :is-error true})))

(defn- run-beta-tools*
  [call-fn params {:keys [max-iterations on-message on-turn]
                   :or {max-iterations 10 on-turn (fn [_ params] params)}}]
  (loop [iterations 0
         params params
         messages (cond
                    (nil? (:messages params)) []
                    (string? (:messages params)) [{:role :user :content (:messages params)}]
                    :else (vec (:messages params)))]
    (when (>= iterations max-iterations)
      (throw (ex-info "Beta tool loop exceeded max iterations"
                      {:anthropic/error :max-iterations-exceeded
                       :iterations iterations
                       :messages messages})))
    (let [fns (beta-tool-fns (:tools params))
          response (call-fn (-> params strip-tool-fns (assoc :messages messages)))
          tool-uses (filterv #(= :tool-use (:type %)) (:content response))]
      (when on-message (on-message response))
      (if (or (= :tool-use (:stop-reason response)) (seq tool-uses))
        (let [results (mapv (fn [{:keys [name] :as block}]
                              (if-let [f (get fns name)]
                                (beta-tool-result block f)
                                (throw (ex-info "Tool call has no matching :fn"
                                                {:anthropic/error :no-tool-fn :name name}))))
                            tool-uses)
              next-messages (conj messages
                                  {:role :assistant :content (:content response)}
                                  {:role :user :content results})
              next-params (on-turn response (assoc params :messages next-messages))]
          (recur (inc iterations)
                 next-params
                 (or (:messages next-params) next-messages)))
        (do
          (on-turn response (assoc params :messages
                                   (conj messages {:role :assistant :content (:content response)})))
          (assoc response :messages (conj messages {:role :assistant :content (:content response)})))))))

(defn run-beta-tools
  "Run beta Messages with local tool functions until no tool is requested.

  Options include `:max-iterations`, `:on-message`, and `:on-turn`. `:on-turn`
  receives each assistant response and the current params, and returns params
  for the next iteration, allowing tools and request settings to change. Tool
  specs support response-inclusion, input-examples, eager-input-streaming,
  caching, and dated :version options."
  ([^AnthropicClient client params]
   (run-beta-tools client params {}))
  ([^AnthropicClient client params opts]
   (run-beta-tools* (partial create-beta-message client) params opts)))

(defn- beta-message-param->map [param]
  (let [m (keywordize-message-enums (keywordize-types (json->clj (JsonValue/from param))))]
    (cond-> m
      (string? (:role m)) (update :role ->keyword)
      (string? (:clear-at m)) (update :clear-at ->keyword))))

(declare beta-stream-event->map)

(defn- beta-tool-runner-handle* [^BetaToolRunner runner]
  (letfn [(stream-response-events [^StreamResponse response]
            (with-open [^StreamResponse stream response]
              (let [events (java.util.ArrayList.)]
                (.forEach (.stream stream)
                          (reify java.util.function.Consumer
                            (accept [_ event] (.add events event))))
                (mapv beta-stream-event->map events))))
          (stream-events [^java.util.Iterator responses]
            (lazy-seq
             (when (.hasNext responses)
               (let [response (.next responses)
                     events (seq (stream-response-events response))]
                 (when events
                   (cons (first events)
                         (concat (rest events)
                                 (stream-events responses))))))))]
    {:messages (fn []
               (map beta-message->map
                    (iterator-seq (.iterator runner))))
     :streaming (fn []
                  (stream-events (.iterator ^Iterable (.streaming runner))))
     :set-next-params! (fn [params]
                         (.setNextParams runner ^MessageCreateParams (->params params)))
     :last-tool-response (fn []
                          (some-> (.lastToolResponse runner)
                                  (.orElse nil)
                                  beta-message-param->map))}))

(defn beta-tool-runner-handle
  "Create a Clojure handle around the SDK's blocking `BetaToolRunner`.

  The handle's `:messages` and `:streaming` operations return lazy sequences;
  `:set-next-params!` accepts a normal request map, and `:last-tool-response`
  returns a translated message-param map or nil. The one-argument form wraps
  an existing SDK runner; the other forms create one through the beta
  Messages service."
  ([^BetaToolRunner runner]
   (beta-tool-runner-handle* runner))
  ([^AnthropicClient client params]
   (beta-tool-runner-handle client params {}))
  ([^AnthropicClient client params opts]
   (let [service (.messages (.beta client))
         runner (.toolRunner service (->params params) (->request-options opts))]
     (beta-tool-runner-handle* runner))))

(defn count-beta-tokens
  "Count beta Messages input tokens without creating a message. Request maps
  support cache-control, compaction, context-management, mcp-servers, response-format,
  output-type, effort, task-budget, output-format, speed, user-profile-id, extra-headers,
  extra-query, and extra-body. Returns :input-tokens and, when present, nested
  :context-management data."
  ([^AnthropicClient client req] (count-beta-tokens client req {}))
  ([^AnthropicClient client req opts]
   (with-api-errors
     (let [params (->count-params req)
           request-options (->request-options opts)]
       (if (:include-response opts)
         (with-open [^HttpResponseFor response (.countTokens (.withRawResponse (.messages (.beta client))) params request-options)]
           (assoc (beta-tokens-count->map (.parse response)) :response (response-metadata response)))
         (beta-tokens-count->map (.countTokens (.messages (.beta client)) params request-options)))))))

(defn- ->batch-request-params ^BatchCreateParams$Request$Params [req]
  (let [^MessageCreateParams p (->params req)
        ^BatchCreateParams$Request$Params$Builder b
        (doto (BatchCreateParams$Request$Params/builder)
          (.maxTokens (.maxTokens p)) (.messages (.messages p)) (.model (.model p)))]
    (doseq [[value setter]
            [[(.cacheControl p) #(.cacheControl b ^BetaCacheControlEphemeral %)]
             [(.inferenceGeo p) #(.inferenceGeo b ^String %)]
             [(.mcpServers p) #(.mcpServers b ^java.util.List %)]
             [(.metadata p) #(.metadata b ^BetaMetadata %)]
             [(.outputConfig p) #(.outputConfig b ^BetaOutputConfig %)]
             [(.stopSequences p) #(.stopSequences b ^java.util.List %)]
             [(.temperature p) #(.temperature b (double %))]
             [(.thinking p) #(.thinking b ^BetaThinkingConfigParam %)]
             [(.toolChoice p) #(.toolChoice b ^BetaToolChoice %)]
             [(.tools p) #(.tools b ^java.util.List %)]
             [(.topK p) #(.topK b (long %))] [(.topP p) #(.topP b (double %))]]]
      (when (.isPresent ^java.util.Optional value) (setter (.get ^java.util.Optional value))))
    (when-let [container (:container req)]
      (if (string? container)
        (.container b ^String container)
        (.container b ^BetaContainerParams (->beta-container-params container))))
    (when-let [system (:system req)]
      (if (string? system) (.system b ^String system)
          (.systemOfBetaTextBlockParams b ^java.util.List (mapv ->system-block system))))
    (when-let [tier (:service-tier req)]
      (.serviceTier b (BatchCreateParams$Request$Params$ServiceTier/of
                       (-> tier name (str/replace "-" "_")))))
    (.build b)))

(defn- ->batch-request ^BatchCreateParams$Request [{:keys [custom-id params]}]
  (-> (BatchCreateParams$Request/builder) (.customId ^String custom-id)
      (.params (->batch-request-params params)) (.build)))

(defn- ->batch-create-params ^BatchCreateParams [{:keys [requests]}]
  (-> (BatchCreateParams/builder) (.requests ^java.util.List (mapv ->batch-request requests)) (.build)))

(defn- batch->map [^BetaMessageBatch batch]
  (let [m (json->clj (JsonValue/from batch))]
    (cond-> m (string? (:type m)) (update :type ->keyword)
      (string? (:processing-status m)) (update :processing-status ->keyword))))

(defn- deleted-batch->map [^BetaDeletedMessageBatch batch]
  (let [m (json->clj (JsonValue/from batch))]
    (cond-> m (string? (:type m)) (update :type ->keyword))))

(defn create-beta-batch [^AnthropicClient client req]
  (with-api-errors
    (let [^BatchService batches (-> (.beta client) (.messages) (.batches))]
      (batch->map (.create batches (->batch-create-params req))))))

(defn get-beta-batch [^AnthropicClient client ^String id]
  (with-api-errors
    (let [^BatchService batches (-> (.beta client) (.messages) (.batches))]
      (batch->map (.retrieve batches id)))))

(defn- ->batch-list-params ^BatchListParams [{:keys [after-id before-id limit betas]}]
  (let [b (BatchListParams/builder)]
    (when after-id (.afterId b ^String after-id))
    (when before-id (.beforeId b ^String before-id))
    (when limit (.limit b (long limit)))
    (doseq [beta betas]
      (let [^String beta-name (if (keyword? beta) (name beta) beta)]
        (.addBeta b beta-name)))
    (.build b)))

(defn list-beta-batches
  ([^AnthropicClient client] (list-beta-batches client {}))
  ([^AnthropicClient client opts]
   (with-api-errors
     (let [^BatchService batches (-> (.beta client) (.messages) (.batches))
           ^BatchListPage page (.list batches (->batch-list-params opts))]
       (mapv batch->map (.autoPager page))))))

(defn list-beta-batches-lazy
  "Lazily list beta message batches; accepts the same options as `list-beta-batches`."
  ([^AnthropicClient client] (list-beta-batches-lazy client {}))
  ([^AnthropicClient client opts]
   (with-api-errors
     (let [^BatchService batches (-> (.beta client) (.messages) (.batches))
           ^BatchListPage page (.list batches (->batch-list-params opts))]
       (pagination/->lazy-pager batch->map (.autoPager page))))))

(defn cancel-beta-batch [^AnthropicClient client ^String id]
  (with-api-errors
    (let [^BatchService batches (-> (.beta client) (.messages) (.batches))]
      (batch->map (.cancel batches id)))))

(defn delete-beta-batch [^AnthropicClient client ^String id]
  (with-api-errors
    (let [^BatchService batches (-> (.beta client) (.messages) (.batches))
          params (-> (BatchDeleteParams/builder) (.messageBatchId id) (.build))]
      (deleted-batch->map (.delete batches params)))))

(defn- batch-result->map [^BetaMessageBatchIndividualResponse response]
  (json->clj (JsonValue/from response)))

(defn- reduce-beta-batch-result-stream [^StreamResponse sr f init]
  (with-open [^StreamResponse stream sr]
    (reduce (fn [acc response] (f acc (batch-result->map response))) init
            (iterator-seq (.iterator (.stream stream))))))

(defn reduce-beta-batch-results [^AnthropicClient client ^String id f init]
  (with-api-errors
    (let [^BatchService batches (-> (.beta client) (.messages) (.batches))]
      (reduce-beta-batch-result-stream (.resultsStreaming batches id) f init))))

(defn beta-batch-results [^AnthropicClient client ^String id]
  (reduce-beta-batch-results client id conj []))

(defn- beta-stream-event->map [^BetaRawMessageStreamEvent event]
  (let [m (json->clj (or (some-> (._json event) (.orElse nil))
                         (JsonValue/from event)))]
    (keywordize-message-enums (cond-> m (string? (:type m)) (update :type ->keyword)))))

(defn- consume-beta-stream ^String [^StreamResponse sr on-event]
  (with-open [^StreamResponse stream sr]
    (let [sb (StringBuilder.)]
      (doseq [event (iterator-seq (.iterator (.stream stream)))]
        (let [m (beta-stream-event->map event)]
          (when-let [text (get-in m [:delta :text])] (.append sb ^String text))
          (when on-event (on-event m))))
      (str sb))))

(defn stream-beta-message ^String [^AnthropicClient client req on-event]
  (with-api-errors
    (consume-beta-stream (.createStreaming (.messages (.beta client)) (->params req)) on-event)))

(defn stream-beta-text ^String [^AnthropicClient client req on-text]
  (stream-beta-message client req
                       (fn [event]
                         (when-let [text (get-in event [:delta :text])]
                           (when on-text (on-text text))))))

(defn stream-beta-message-handle
  "Start a beta Messages SSE stream on a worker thread and return a
  cancellable handle. `on-event` receives normalized event maps."
  ([^AnthropicClient client req on-event]
   (stream-beta-message-handle client req on-event {}))
  ([^AnthropicClient client req on-event opts]
   (stream-control/start!
    #(.createStreaming (.messages (.beta client)) (->params req))
    on-event
    (assoc (or opts {}) :map-event beta-stream-event->map))))

(defn stream-beta-message-queue
  "Start a bounded pull stream of normalized beta Messages events."
  ([^AnthropicClient client req] (stream-beta-message-queue client req {}))
  ([^AnthropicClient client req opts]
   (stream-beta-message-handle client req nil
                               (assoc (or opts {}) :buffer-size (or (:buffer-size opts) 64)))))

(def cancel-stream! stream-control/cancel-stream!)
(def close-stream! stream-control/close-stream!)
(def take-stream-event stream-control/take-stream-event)
(def await-stream stream-control/await-stream)
